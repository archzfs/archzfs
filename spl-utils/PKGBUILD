# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname="spl-utils"
#
# This script doesn't use bash variables for the version number because AUR
# doesn't properly parse bash variables. We use a custom build script to
# automatically change the appropriate variables before building in a clean
# chroot environment (using systemd-ndspawn).
#
# The build script can be found at
# https://github.com/demizer/archzfs/blob/master/build.sh
#
pkgver=0.6.2_3.14
pkgrel=2
pkgdesc="Solaris Porting Layer kernel module support files."
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/spl/spl-0.6.2.tar.gz
        20140411-git-master.patch
        spl-utils.hostid)
groups=("archzfs")
md5sums=('f00535bf89a7fde0e08f44a14a1f1e03'
         '5393794838f95061ccd10dca2553bbf5'
         'a54f0041a9e15b050f25c463f1db7449')
license=("GPL")

prepare() {
    cd "$srcdir/spl-0.6.2"
    patch -Np1 < ../20140411-git-master.patch
}

build() {
  cd "$srcdir/spl-0.6.2"
  ./autogen.sh
  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
                --with-config=user
  else
    ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
                --with-config=user
  fi
  make
}

package() {
  cd "$srcdir/spl-0.6.2"
  make DESTDIR="$pkgdir" install

  install -D -m644 "$srcdir"/spl-utils.hostid "$pkgdir"/etc/hostid
}
