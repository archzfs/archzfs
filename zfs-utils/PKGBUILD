# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("zfs-utils")
#
# This script doesn't use bash variables for the version number because AUR
# doesn't properly parse bash variables. We use a custom build script to
# automatically change the appropriate variables before building in a clean
# chroot environment (using systemd-ndspawn).
#
# The build script can be found at
# https://github.com/demizer/archzfs/blob/master/build.sh
#
pkgver=0.6.2_3.12.1
pkgrel=1
pkgdesc="Kernel module support files for the Zettabyte File System."
depends=("spl")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-0.6.2.tar.gz
        zfs-utils.bash-completion
        zfs-utils.initcpio.install
        zfs-utils.initcpio.hook
        zfs-utils.service)
groups=("archzfs")
md5sums=('0b183b0abdd5be287046ad9ce4f899fd'
         '3e1c4a29c4f7d590e6a3041f2c61d6ff'
         'ebea2b1e593a9a19efa0f7ae7451bbc7'
         '9a4083058ded206783c921e9ac7afdcf'
         '1f16ea8592509edb8efa4d7da25f8182')
license=("CDDL")

build() {
  cd "$srcdir/zfs-0.6.2"
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-0.6.2 \
              --with-config=user
  make
}

package() {
  cd "$srcdir/zfs-0.6.2"
  make DESTDIR="$pkgdir" install

  # Remove uneeded files
  rm -r "$pkgdir"/etc/init.d
  rm -r "$pkgdir"/usr/lib/dracut

  # move module tree /lib -> /usr/lib
  cp -r "$pkgdir"/{lib,usr}
  rm -r "$pkgdir"/lib

  # Fixup path
  mv "$pkgdir"/sbin/mount.zfs "$pkgdir"/usr/bin/
  rm -r "$pkgdir"/sbin

  install -D -m644 "$srcdir"/zfs-utils.initcpio.hook "$pkgdir"/usr/lib/initcpio/hooks/zfs
  install -D -m644 "$srcdir"/zfs-utils.initcpio.install "$pkgdir"/usr/lib/initcpio/install/zfs
  install -D -m644 "$srcdir"/zfs-utils.service "$pkgdir"/usr/lib/systemd/system/zfs.service
  install -D -m644 "$srcdir"/zfs-utils.bash-completion "$pkgdir"/usr/share/bash-completion/completions/zfs
}
