# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("zfs")
pkgver=0.6.2_3.10.9
pkgrel=1
pkgdesc="Kernel modules for the Zettabyte File System."
depends=("spl=0.6.2_3.10.9-1" "zfs-utils=0.6.2_3.10.9-1" "linux=3.10.9-1")
makedepends=("linux-headers=3.10.9-1")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-0.6.2.tar.gz)
groups=("archzfs")
md5sums=('0b183b0abdd5be287046ad9ce4f899fd')
license=("CDDL")
install=zfs.install

build() {
  cd "$srcdir/zfs-0.6.2"
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-0.6.2 \
              --with-config=kernel
  make
}

package() {
  cd "$srcdir/zfs-0.6.2"
  make DESTDIR="$pkgdir" install

  # move module tree /lib -> /usr/lib
  cp -r $pkgdir/{lib,usr}
  rm -r $pkgdir/lib
}
