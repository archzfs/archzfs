# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname="zfs"
#
# This script doesn't use bash variables for the version number because AUR
# doesn't properly parse bash variables. We use a custom build script to
# automatically change the appropriate variables before building in a clean
# chroot environment (using systemd-ndspawn).
#
# The build script can be found at
# https://github.com/demizer/archzfs/blob/master/build.sh
#
pkgver=0.6.2_3.13.4
pkgrel=1

# Used incase the i686 and x86_64 linux packages get out of sync with the
# PKGREL. This occurred on January 31, 2014 where i686 was versioned at
# 3.12.9-1 and x86_64 was versioned at 3.12.9-2.
LINUX_VERSION_X32="3.13.4-1"
LINUX_VERSION_X64="3.13.4-1"
[[ $CARCH == "i686" ]] && LINUX_VERSION=$LINUX_VERSION_X32 || LINUX_VERSION=$LINUX_VERSION_X64

pkgdesc="Kernel modules for the Zettabyte File System."
depends=("spl=0.6.2_3.13.4-1" "zfs-utils" "linux=$LINUX_VERSION")
makedepends=("linux-headers=$LINUX_VERSION")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-0.6.2.tar.gz)
groups=("archzfs")
md5sums=('0b183b0abdd5be287046ad9ce4f899fd')
license=("CDDL")
install=zfs.install

build() {
  cd "$srcdir/zfs-0.6.2"
  ./autogen.sh
  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/lib/udev \
                --libexecdir=/usr/lib/zfs-0.6.2 \
                --with-config=kernel \
                --with-linux=/usr/lib/modules/$LINUX_VERSION_X32-ARCH/build
  else
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/lib/udev \
                --libexecdir=/usr/lib/zfs-0.6.2 \
                --with-config=kernel \
                --with-linux=/usr/lib/modules/$LINUX_VERSION_X64-ARCH/build
  fi
  make
}

package() {
  cd "$srcdir/zfs-0.6.2"
  make DESTDIR="$pkgdir" install

  # move module tree /lib -> /usr/lib
  cp -r "$pkgdir"/{lib,usr}
  rm -r "$pkgdir"/lib
}
